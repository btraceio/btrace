plugins {
  id 'java'
  id("me.champeau.gradle.jmh") version "0.5.0"
}

description 'A JMH benchmark to assert the overhead imposed by various types of BTrace instrumentation.'

def env = System.getenv()
def javaHome = env['JAVA_HOME']

dependencies {
  compile project(path: ":btrace-dist", configuration: "shadow")
  compile project(":btrace-compiler")
  jmh project(":btrace-instr")
  jmh project(":btrace-runtime")
  jmh project(":btrace-statsd")
  jmh("org.openjdk.jmh:jmh-core:1.21")
  jmh("org.openjdk.jmh:jmh-generator-annprocess:1.21")
}

task btracec(type: JavaExec) {
  group 'Build'
  inputs.files 'src/main/resources/scripts'
  outputs.dir "${buildDir}/classes/main"

  environment('BTRACE_HOME', "$projectDir")
  classpath configurations.compile
  main 'org.openjdk.btrace.compiler.Compiler'
  args '-d'
  args "${buildDir}/classes/java/jmh/"
  args '-packext'
  args 'btclass'
  args fileTree(dir: "src/jmh/btrace", include: 'TraceScript.java')
}
jmhClasses.dependsOn btracec

jmhJar {
  include 'META-INF/BenchmarkList'
  include 'META-INF/CompilerHints'
  include 'org/openjdk/jmh/**'
  include 'org/openjdk/btrace/BTraceBench.class'
  include 'org/openjdk/btrace/generated/**/*'
  include 'jmh*'
  include '*.btclass'
}

jmh {
  duplicateClassesStrategy = 'warn'
  jvmArgsAppend = "-Djmh.basedir=${project.buildDir}/.. -Dproject.version=${project.version}"
}

