plugins {
  id 'java'
  id("me.champeau.jmh") version "0.6.2"
}

description 'A JMH benchmark to assert the overhead imposed by various types of BTrace instrumentation.'

def env = System.getenv()
def javaHome = env['JAVA_HOME']

dependencies {
  compile project(path: ":btrace-dist", configuration: "shadow")
  compile project(":btrace-compiler")
  jmh project(":btrace-instr")
  jmh project(":btrace-runtime")
  jmh project(":btrace-statsd")
  jmh("org.openjdk.jmh:jmh-core:1.29")
  jmh("org.openjdk.jmh:jmh-generator-annprocess:1.27")
}

task btracec(type: JavaExec) {
  group 'Build'
  inputs.files 'src/main/resources/scripts'
  outputs.dir "${buildDir}/classes/java/main"

  environment('BTRACE_HOME', "$projectDir")
  classpath configurations.compile
  main 'org.openjdk.btrace.compiler.Compiler'
  args '-d'
  args "${buildDir}/classes/java/main/"
  args '-packext'
  args 'btclass'
  args fileTree(dir: "src/jmh/btrace", include: 'TraceScript.java')
}
jmhClasses.dependsOn btracec

jmhJar {
  include 'META-INF/BenchmarkList'
  include 'META-INF/CompilerHints'
  include 'org/openjdk/jmh/**'
  include 'org/openjdk/btrace/bench/**/*.class'
  include 'org/openjdk/btrace/generated/**/*'
  include "joptsimple/**"
  include "org/apache/**"
  include 'jmh*'
  include 'benchmark/**'
  include '*.btclass'
}

jmh {
  warmupIterations = 5
  iterations = 10
  fork = 2
  jvm = "${env['JAVA_HOME']}/bin/java"
  duplicateClassesStrategy = DuplicatesStrategy.WARN
  def agent = "-javaagent:${project.buildDir}/../../../btrace-dist/build/resources/main/${project.version}/libs/btrace-agent.jar=stdout=true,noServer=true,debug=true,script=${project.buildDir}/classes/java/main/TraceScript.btclass"
  println(agent)
  jvmArgsAppend = ["\"-Djmh.basedir=${project.buildDir}/.. -Dproject.version=${project.version} -Xmx128m ${agent.toString()}\""]
  includes = ['.*BTraceBench.*']
}