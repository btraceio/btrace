sourceSets {

    java9 {
        java {
            srcDirs = ['src/main/java9']
        }
    }
    java11 {
        java {
            srcDirs = ['src/main/java11']
        }
    }
}

compileJava {
    sourceCompatibility = 7
    targetCompatibility = 7
    options.fork = true
    options.forkOptions.executable = "${project.property("JAVA_8_HOME")}/bin/javac"
}

compileJava9Java {
    sourceCompatibility = 9
    targetCompatibility = 9
    options.fork = true
    options.forkOptions.executable = "${project.property("JAVA_9_HOME")}/bin/javac"
    options.compilerArgs.addAll(['--add-exports', 'java.base/jdk.internal.reflect=ALL-UNNAMED', '--add-exports', 'java.base/jdk.internal.perf=ALL-UNNAMED'])
}

compileJava11Java {
    sourceCompatibility = 11
    targetCompatibility = 11
    options.fork = true
    options.forkOptions.executable = "${project.property("JAVA_11_HOME")}/bin/javac"
    options.compilerArgs.addAll(['--add-exports', 'java.base/jdk.internal.reflect=ALL-UNNAMED', '--add-exports', 'java.base/jdk.internal.perf=ALL-UNNAMED'])
}

dependencies {
    java9Implementation files(sourceSets.main.output.classesDirs) {
        builtBy compileJava
    }
    java11Implementation files(sourceSets.main.output.classesDirs) {
        builtBy compileJava
    }

    java9Implementation project(':btrace-core')
    java9Implementation project(':btrace-services')

    java11Implementation project(':btrace-core')
    java11Implementation project(':btrace-services')

    compile project(':btrace-core')
    compile project(':btrace-services')
}

jar {
    into('') {
        from sourceSets.java9.output
    }
    into('') {
        from sourceSets.java11.output
    }
    manifest.attributes('Multi-Release': 'true')
}