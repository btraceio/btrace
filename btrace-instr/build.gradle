import java.nio.file.Files
import java.nio.file.Paths

plugins {
    id 'java'
}

def downloadedJar = { url, name ->
    File file = new File("$buildDir/download/${name}.jar")
    file.parentFile.mkdirs()
    if (!file.exists()) {
        new URL(url).withInputStream { downloadStream ->
            file.withOutputStream { fileOut ->
                fileOut << downloadStream
            }
        }
    }
    files(file.absolutePath)
}

dependencies {
    implementation libs.slf4j
    implementation libs.asm
    implementation libs.asm.tree
    implementation libs.autoService
    implementation libs.jctools
    implementation project(':btrace-core')
    implementation project(':btrace-services-api')
    implementation project(':btrace-runtime')
    implementation project(':btrace-compiler')
    implementation project(':btrace-statsd')

    testImplementation libs.asm.util
    testImplementation libs.slf4j.simple
    testImplementation libs.junit.jupiter
    testImplementation project(':btrace-client')

    // https://mvnrepository.com/artifact/org.scala-lang/scala-library
    testImplementation group: 'org.scala-lang', name: 'scala-library', version: '2.13.10'

    testImplementation downloadedJar("https://github.com/renaissance-benchmarks/renaissance/releases/download/v0.14.1/renaissance-gpl-0.14.1.jar", "renaissance-gpl-0.14.1")
}

compileTestJava {
    sourceCompatibility = 8
    targetCompatibility = 8
    options.fork = true
    options.forkOptions.executable = "${project.property("JAVA_8_HOME")}/bin/javac"
}

task compileTestProbes {
    dependsOn compileTestJava
    doLast {
        def path = project(':btrace-instr').sourceSets.main.runtimeClasspath

        def loader = new URLClassLoader(path.collect { f -> f.toURL() } as URL[])
        def compiler = loader.loadClass('org.openjdk.btrace.compiler.Compiler')
        def rtCp = sourceSets.main.runtimeClasspath

        def args = ["-cp", rtCp.plus(files(buildDir.toPath().resolve("classes/java/test"), buildDir.toPath().resolve("classes/java/java11_dummy"))).getAsPath(), "-d", buildDir.toPath().resolve("classes")]

        def files = fileTree(dir: "src/test/btrace", include: '**/*.java', exclude: 'verifier/**/*.java').findAll {
            it != null
        }.collect { it }

        args.addAll(files)

        compiler.main(args as String[])
    }
}

tasks.withType(Test) {
    useJUnitPlatform()
}

task regenerateGoldenFiles(type: Test) {
    inputs.files compileTestProbes.outputs
    testLogging.showStandardStreams = true

    def props = new Properties()
    props.load(Files.newInputStream(Paths.get(System.getenv("JAVA_HOME"), "release")))
    if (props.getProperty("JAVA_VERSION")?.contains("1.8")) {
        jvmArgs "-Dproject.test.resources=${sourceSets.test.resources.srcDirs[0]}", "-Dregenerate.golden=true", "-Dproject.version=${project.version}"
    } else {
        jvmArgs "-Dproject.test.resources=${sourceSets.test.resources.srcDirs[0]}", "-Dregenerate.golden=true", '-XX:+IgnoreUnrecognizedVMOptions', '--add-opens', 'java.base/jdk.internal.reflect=ALL-UNNAMED', '--add-exports', 'java.base/jdk.internal.reflect=ALL-UNNAMED', "-Dproject.version=${project.version}"
    }

    filter {
        includeTestsMatching "OnMethodInstrumentorTest"
    }
}

test {
    dependsOn cleanTest
    inputs.files compileTestProbes.outputs
    testLogging.showStandardStreams = true

    def props = new Properties()
    props.load(Files.newInputStream(Paths.get(System.getenv("JAVA_HOME"), "release")))
    if (props.getProperty("JAVA_VERSION")?.contains("1.8")) {
        jvmArgs "-Dproject.test.resources=${sourceSets.test.resources.srcDirs[0]}", "-Dregenerate.golden=${System.getProperty('regenerate.golden')}", "-Dproject.version=${project.version}"
    } else {
        jvmArgs "-Dproject.test.resources=${sourceSets.test.resources.srcDirs[0]}", "-Dregenerate.golden=${System.getProperty('regenerate.golden')}", '-XX:+IgnoreUnrecognizedVMOptions', '--add-opens', 'java.base/jdk.internal.reflect=ALL-UNNAMED', '--add-exports', 'java.base/jdk.internal.reflect=ALL-UNNAMED', "-Dproject.version=${project.version}"
    }
}