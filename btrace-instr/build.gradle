import java.nio.file.Files
import java.nio.file.Paths

apply plugin: 'java'

buildscript {
    dependencies {
        classpath files('../btrace-compiler/build/classes/main/java')
    }
}

sourceSets {
    java11_dummy {
        java {
            srcDirs = ['src/main/java11_dummy']
        }
    }
}

dependencies {
    implementation group: 'org.ow2.asm', name: 'asm', version: "${rootProject.asmVersion}"
    implementation group: 'org.ow2.asm', name: 'asm-tree', version: "${rootProject.asmVersion}"
    implementation group: 'com.google.auto.service', name: 'auto-service', version: '1.0.1'

    implementation project(':btrace-core')
    implementation project(':btrace-services-api')
    implementation project(':btrace-runtime')
    implementation project(':btrace-compiler')
    implementation project(':btrace-statsd')

    // https://mvnrepository.com/artifact/org.jctools/jctools-core
    implementation(group: 'org.jctools', name: 'jctools-core', version: '3.3.0')

    testImplementation project(':btrace-client')
    testImplementation("org.junit.jupiter:junit-jupiter-params:5.8.2")
    testImplementation group: 'org.ow2.asm', name: 'asm-util', version: "${rootProject.asmVersion}"
}

compileJava11_dummyJava {
    sourceCompatibility = 8
    targetCompatibility = 8
    options.fork = true
    options.forkOptions.executable = "${project.property("JAVA_11_HOME")}/bin/javac"
    options.forkOptions.javaHome = file("${project.property("JAVA_11_HOME")}")
}

compileTestJava {
    sourceCompatibility = 8
    targetCompatibility = 8
    options.fork = true
    options.forkOptions.executable = "${project.property("JAVA_8_HOME")}/bin/javac"
}

task compileTestProbes {
    dependsOn compileTestJava, compileJava11_dummyJava
    doLast {
        def path = project(':btrace-instr').sourceSets.main.runtimeClasspath

        def loader = new URLClassLoader(path.collect { f -> f.toURL() } as URL[])
        def compiler = loader.loadClass('org.openjdk.btrace.compiler.Compiler')
        def rtCp = sourceSets.main.runtimeClasspath.collect { it.absolutePath }.join(':')

        def args = ["-cp", "${buildDir}/classes/java/test:${buildDir}/classes/java/java11_dummy:${rtCp}", "-d", "${buildDir}/classes"]

        def files = fileTree(dir: "src/test/btrace", include: '**/*.java', exclude: 'verifier/**/*.java').findAll {
            it != null
        }.collect { it }

        args.addAll(files)

        compiler.main(args as String[])
    }
}

task buildEventsJar(type: Jar) {
    inputs.files compileJava11_dummyJava
    from files("${project.buildDir}/classes/java/java11_dummy")
    archiveBaseName = "events"
    archiveVersion = ""
    archiveClassifier = ""
}

test {
    onlyIf {
        new File("${projectDir}/../btrace-dist/build/resources/main/v${project.version}/libs/btrace-client.jar").exists()
    }
    dependsOn cleanTest, buildEventsJar
    inputs.files compileTestProbes.outputs
    //inputs.files buildDTrace.outputs
    testLogging.showStandardStreams = true

    def props = new Properties()
    props.load(Files.newInputStream(Paths.get(System.getenv("JAVA_HOME"), "release")))
    if (props.getProperty("JAVA_VERSION")?.contains("1.8")) {
        jvmArgs "-Dproject.version=${project.version}"
    } else {
        jvmArgs '-XX:+IgnoreUnrecognizedVMOptions', '--add-opens', 'java.base/jdk.internal.reflect=ALL-UNNAMED', '--add-exports', 'java.base/jdk.internal.reflect=ALL-UNNAMED', "-Dproject.version=${project.version}"
    }
}