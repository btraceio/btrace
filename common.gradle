//
// This file is to be applied to every subproject.
//

apply plugin: 'java'
apply plugin: 'idea'

project.group = 'org.openjdk.btrace'
project.version = '2.2.6'

buildscript {
    repositories {
        mavenCentral()
    }
}

ext.getToolsJar = {
    return javaToolchains.compilerFor {
        languageVersion.set(JavaLanguageVersion.of(11))
    }.get().metadata.installationPath.file("lib/tools.jar")
}

ext.getJavac = { int version = 11 ->
    return javaToolchains.compilerFor {
        languageVersion.set(JavaLanguageVersion.of(version))
    }.get().metadata.installationPath.file("bin/javac")
}

ext.getJavadoc = {
    return javaToolchains.compilerFor {
        languageVersion.set(JavaLanguageVersion.of(8))
    }.get().metadata.installationPath.file("bin/javadoc")
}

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

compileJava {
    sourceCompatibility = 8
    targetCompatibility = 8
    javaCompiler = javaToolchains.compilerFor {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
}

repositories {
    mavenCentral()
    // You may define additional repositories, or even remove "mavenCentral()".
    // Read more about repositories here:
    //   http://www.gradle.org/docs/current/userguide/dependency_management.html#sec:repositories
}

dependencies {
    // Adding dependencies here will add the dependencies to each subproject.
	testImplementation libs.junit.jupiter
}

String mavenArtifactId = name

javadoc {
    options.addStringOption('Xdoclint:all,-missing', '-quiet')
    options.encoding("UTF-8")
    executable = "${getJavadoc()}"
    failOnError true
}

task sourcesJar(type: Jar, dependsOn: classes, description: 'Creates a jar from the source files.') {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

task createFolders(description: 'Creates the source folders if they do not exist.') doLast {
    sourceSets*.allSource*.srcDirs*.each { File srcDir ->
        if (!srcDir.isDirectory()) {
            println "Creating source folder: ${srcDir}"
            srcDir.mkdirs()
        }
    }
}

test {
    useJUnitPlatform()
    // Always run tests, even when nothing changed.
    dependsOn 'cleanTest'

    // Show test results.
    testLogging {
        events "passed", "skipped", "failed"
    }
}